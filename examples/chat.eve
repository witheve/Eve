add a user for this sesion on connect
  [#session-connect]
  freeze
    [#user @me]

build the chat pane
  channel = "general"
  maintain
    [#div class: "chat-pane" children:
      [#div #chat-messages class: "chat-messages" channel]
      [#input #channel-input autofocus: true channel]]

draw messages
  parent = [#chat-messages channel]
  id = [#message name time message channel]
  maintain
    parent.children += [#div id, class: "chat-message", children:
                         [#div class: "chat-user", text: name]
                         [#div class: "chat-time", text: time]
                         [#div class: "chat-message", text: message]]

handle chat keydowns
  event = [#keydown element, key: "enter"]
  element = [#channel-input channel value]
  [#user name]
  [#time hours minutes seconds]
  freeze
    element.value := ""
  freeze all
    [#message event name, time: "{hours}:{minutes}", message: value, channel]
