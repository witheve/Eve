Initialize board
  [#session-connect]

  // board constants
  width = 3
  height = 3
  n-in-a-row = 3
  starting-player = "x"

  // generate the cells
  i = range(0, width * height)
  column = mod(i, width)
  row = floor(i / width)
  diag-left = column - row
  diag-right = (width - column) - row

  freeze
    board = [@board width height n-in-a-row player: starting-player]
    [#cell board row column diag-left diag-right]

Click on a cell to make your move
  [#click #direct-target element: [#div cell]]
  not(cell.player)
  board = [@board player: current not(winner)]
  next_player = if current = "x" then "o"
                else "x"
  freeze
    board.player := next_player
    cell.player := current

Get N in a row to win the game!
  board = [@board n-in-a-row not(winner)]
  winner = if cell = [#cell row player]
              n-in-a-row = count(given cell per row, player) then player
           else if cell = [#cell column player]
              n-in-a-row = count(given cell per column, player) then player
           else if cell = [#cell diag-left player]
              n-in-a-row = count(given cell per diag-left, player) then player
           else if cell = [#cell diag-right player]
              n-in-a-row = count(given cell per diag-right, player) then player
  freeze
    board.winner := winner

Reset the board state after a win
  [#click #direct-target]
  board = [@board winner]
  cell = [#cell player]
  freeze
    board.winner -= winner
    cell.player -= player

Draw the board
  board = [@board]
  cell = [#cell board row column]
  contents = if cell.player then cell.player
             else ""
  maintain
    [#div board @container children:
      [#div #status board class: "status"]
      [#div class: "board" children:
        [#div class: "row" sort: row children:
          [#div class: "cell" cell text: contents sort: column style:
            [display: "inline-block" width: "50px" height: "50px" border: "1px solid rgb(47, 47, 49)" color: "black" background: "white" font-size: "2em" line-height: "50px" text-align: "center"]]]]]

Draw the current player
  status = [#status board]
  not(board.winner)
  maintain
    status.text += "{{board.player}}'s turn!"

Draw the winner
  status = [#status board]
  winner = board.winner
  maintain
    status.text += "Player {{winner}} wins! Click anywhere to restart!"
