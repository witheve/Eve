# HTTP Requests

This is a simple example that sends an HTTP request to a website. The website that returns a JSON result. The result in this case is a set of data about some people. We parse those results and format them for display.

Draw a button, that when clicked will send the HTTP request

```
bind @browser
  [#button style: [height: 40], text: "Send an HTTP Request"]
```

## Send a request

On click, send the HTTP request. We're sending it to a service that sends back dummy data in a JSON format. We're expecting a list of users and some data about them.

```
search @event @browser @session
  [#click element: [#button]]

commit @http
  [#request #test-data url: "https://jsonplaceholder.typicode.com/users/"]
```

Display a message while the request is awaiting a response. If you're on a fast connection, you probably won't even see this message. Try disabling your wifi and sending a request, and you'll see this message displayed.

```
search @http
  r = [#request not(response)]
  total = count[given: r]

bind @browser
  [#div text: "Requests in flight: {{total}}"]
```

## Parse a response

We get back a list of users with the following JSON structure:

`{ id, username, email, address: { street, suite, city, zipcode, geo: { lat, lng }}, phone, website, company: { name, catchPhrase, bs }}`

We're only going to display a couple of the attributes, so we can take only the ones we care about. Order doesn't matter

```
search @http
  [#request #test-data response: [json]]
  json = [#array]
  lookup[record: json, attribute, value: [id name email phone website address: [street suite city zipcode]]]

commit
  [#user id name email phone website address: [street suite city zip: zipcode]]
```

Display the responses in a formatted list. 

```
search
  [#user id name email address phone website]

bind @browser
  [#div text: "Results: {{count[given: id]}}"] 
  [#div sort: id, children: 
    [#h3 text: "{{id}}: {{name}}"]
    [#div text: "e-mail: {{email}}"]
    [#div text: "phone: {{phone}}"]
    [#div text: "website:" children: 
      [#a href: website, text: website]]
    [#div text: "address: {{address.street}}, {{address.city}} {{address.zip}}"]]  
```
