Pre-fetch the code for hard-coded watchers
```
  commit
    [#watcher @browser]
    //[#watcher @editor]
```

```
  match
    watcher = [#watcher name]
    not(watcher.code)
  match @file
    [#root child: [@evesrc child: [name: "{{name}}.eve" contents: code]]]
  commit
    watcher.code := code
```


start path resolution
```
  match @file
     fr = [#root]
  match
     connection = [#http-request]
     split[text:r.request.url token index, by:"/"]
     websocket = if r.request.headers.Sec-WebSocket-Protocol = "eve-json" then true else false
  bind
     [#path index:1 file:fr]
     [#path index token]
  commit
     [#request-object connection websocket]
```

resolve a path
```
match
    [#path index:pindex file:pfile]
match @file
    f = pfile.child
    token = f.name
match
    child = [#path index:(pindex + 1) token]
bind
    child.file := f
```

path search complete
```
match
     [#path index token]
     final = [#path index: count[given: index] file]
     split[text:final.token by:"." token:extoken index:exindex]
     exindex = 2
     // since this object is part of the event context, its
     // not avilable when we're looking for it in send eve bootsrap

commit
     [#complete-object extension:extoken token:final.token file]
```


assert the content type table in the same t as the content response
```
match
     [#complete-object]
bind
     [#content-type suffix:"jpeg" type:"application/jpeg"]
     [#content-type suffix:"css" type:"text/css"]
     [#content-type suffix:"js" type:"application/javascript"]
     [#content-type suffix:"html" type:"text/html"]
```

complete! send content response
```
  match
     r = [#request-object connection websocket:false]
     c = [#complete-object extension file]
     ct = [#content-type suffix:extension]
  match @file
     conts = file.contents
  commit
    connection.response := [
       content: conts
       status:"200"
       reason:"OK"
       header:[Content-Type:ct.type]]
    r := none
    c := none
```


send eve bootstrap
```
  match
     r = [#request-object connection websocket:false]
     c = [#complete-object token extension:"eve" file]
  match @file
     [#root child:[name:"index.html" contents]]
  commit
     connection.response := [
       content: contents
       status:"200"
       reason:"OK"
       header:[Content-Type:"text/html"]]
     r := none
     c := none
```

start eve program
```
  match
    r = [#request-object connection  websocket:true]
    c = [#complete-object token extension:"eve" file]

    [#watcher @browser code: browser-code]

  match @file
    source = file.contents

  // there may be a terser version here
  commit @bag
        browser = [#bag r code: browser-code]
        session = [#bag r]
        all = [#bag @all]

  commit @process
     child = [#process source name:token
              scope:[write:session
              read:(browser, session)
              bags:[browser, session, all]]]
  commit
     connection.upgrade := child
     r := none
     c := none
```


no such file!
```
   match
      not ([#complete-object])
      r = [#request-object connection]
   commit
      connection.response :=
       [status:"404"
        reason:"No such object"
        header: [Spoogle: "nothing"]
        content: ""]
      r := none
```

Create a globally shared bag named all

```
  commit @bag
    [#bag @all]
```
